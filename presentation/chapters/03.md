class: center, middle

# Exercises

---

## Exercises &mdash; 1. Connect to the Internet

Our first exercise won't have a physical circuit. We'll be using `wttr.in`,
an awesome weather forecast service to test our connectivity to the internet.

### NodeMCU Development Board

.center[<img src="/images/nodemcu_pins.png" width="400" />]

---

### Establishing connectivity to a wireless network

```c
#include <ESP8266WiFi.h>

#define WLAN_SSID "my awesome wlan name"
#define WLAN_PASS "hunter2"

void setup() {
  Serial.begin(115200);
  Serial.print("Connecting to wireless network ");
  Serial.println(WLAN_SSID);

  WiFi.begin(WLAN_SSID, WLAN_PASS);
  while(WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  IPAddress ip = WiFi.localIP();
  Serial.print("Connected! IP Address: ");
  Serial.println(ip);
}
```

---

### Querying wttr.in

No high-level HTTP clients ðŸ˜§. We'll do everything through a TCP socket ðŸ‘Œ

A simple HTTP request looks like:

```http
GET /Porto?T HTTP/1.1
Host: wttr.in
User-Agent: curl
Connection: close
```
âš ï¸ Each of the lines above is terminated with CRLF characters! (`\r\n`).
**Don't forget the blank line in the end** that marks the end of the HTTP request.

Try it out! Send these lines to `wttr.in` with `nc` or `telnet`!

```bash
nc wttr.in 80
```

---

```c
void loop() {
  // `client` is a TCP client socket which we connect to `wttr.in` in port 80
  WiFiClient client;
  if (!client.connect("wttr.in", 80)) {
    Serial.println("connection failed");
    return;
  }
  // send the HTTP request to the server we just connected to
  client.print("GET /Porto?T HTTP/1.1\r\n");
  client.print("Host: wttr.in\r\n");
  client.print("User-Agent: curl\r\n");
  client.print("Connection: close\r\n");
  client.print("\r\n");
  // wait up to 5 seconds for a response
  unsigned long timeout = millis();
  while (client.available() == 0) {
    if (millis() - timeout > 5000) {
      client.stop();
      return;
    }
  }
  // read all the lines of the reply from server and print them to Serial
  while(client.available()){
    String line = client.readStringUntil('\r');
    Serial.print(line);
  }
  Serial.println();
  // wait before finishing the loop (and starting the next)
  delay(5000);
}
```

---

## Exercises &mdash; 2. Toggle an LED using a web interface

This exercise is a bit different from exercise 1. Instead of using a client socket
to have our microcontroller connect to an internet server, we're using a server
socket so we can have a browser connect to our microcontroller that will act as
an HTTP server.

We'll also set up a small physical circuit using a resistor and an led.

<div style="centered">
  <img src="/images/exercise_2.png" width="300" />
</div>

---

## Breadboard

<img src="/images/breadboards.jpg" width="100%" />

### LED
<img src="/images/led.png" />

### RGB LED
<img src="/images/rgb_led.png" />

### Resistors
---

We'll be using a few functions to reduce the size of the code samples
in this presentation: `connectToWifi`, `printWifiStatus`, `beginHTML` `query`.

```c
// A TCP server listening on port 80 using the wifi hardware
WiFiServer server(80);

void setup() {
  // Initialize serial I/O
  Serial.begin(115200);

  // Initialize GPIO pins
  pinMode(D6, OUTPUT);

  // Connect to WiFi network
  connectToWiFi(WLAN_SSID, WLAN_PASS);
  printWifiStatus();

  // Start the server
  server.begin();
  Serial.println("\nServer started");

  // Get the first line of the HTTP request
  String request = client.readStringUntil('\r');

  // Ignore browser requests for favicon.ico
  if (request.indexOf("favicon.ico") >= 0) return;
}
```

---

```c
void loop() {
  // Wait until a client connects to our server
  WiFiClient client = server.available();
  if (!client) {
    return;
  }

  // Wait until the connected client sends the request data
  while (!client.available()) {
    delay(1);
  }

  // Get the first line of the HTTP request
  String request = client.readStringUntil('\r');

  // Ignore browser requests for favicon.ico
  if (request.indexOf("favicon.ico") >= 0) {
    return;
  }

  ...
```

---

```c
  ...

  // Parse the query string in the GET line
  // GET /?led=ON
  String led = query(request, "led");
  bool isOn = led.indexOf("ON") > -1;

  // Turn LED on or off
  digitalWrite(LED, isOn ? HIGH : LOW);

  // Send HTTP reply to the client
  client.println("HTTP/1.1 200 OK");
  client.println("Content-Type: text/html");
  client.println();
  client.println(beginHTML());
  client.println("<form>");
  client.println(input("hidden", "led", isOn ? "OFF" : "ON"));
  client.println(submit(isOn ? "Turn Off" : "Turn On"));
  client.println("</form>");
  client.println(endHTML());
}

```

---

## Exercises &mdash; 3. Control an RGB LED's hue and brightness using a web interface

This exercise is very similar to the previous, with some more added complexity
in the code and in the circuit.

The RGB led we're providing is the "common anode" type, so we wire it like the
following diagram.

<div style="text-align: center;">
  <img src="/images/exercise_3.png" width="300" />
</div>

---

```c
#define R D5
#define G D6
#define B D7

WiFiServer server(80);

void setup() {
  Serial.begin(115200);

  pinMode(R, OUTPUT);
  pinMode(G, OUTPUT);
  pinMode(B, OUTPUT);

  // Connect to WiFi network
  connectToWiFi(WLAN_SSID, WLAN_PASS);
  printWifiStatus();

  // Start the server
  server.begin();
  Serial.println("\nServer started");
}

```

---

```c
void loop() {
  WiFiClient client = server.available();

  if (!client) {
    return;
  }

  // Wait until de client sends some data
  while(!client.available()) {
    delay(1);
  }

  // process request
  String request = client.readStringUntil('\r');

  // ignore browser requests for favicon.ico
  if (request.indexOf("favicon.ico") >= 0) {
    return;
  }

  ...
```

---

```c
  ...

  // Read the R, G and B values from the query string
  String r = query(request, "r");
  String g = query(request, "g");
  String b = query(request, "b");

  Serial.println("(" + r + ", " + g + ", " + b + ")");

  // For commomn anode type RGB LEDS, lower values mean a brighter ligh:
  analogWrite(R, 1023 - r.toInt());
  analogWrite(G, 1023 - g.toInt());
  analogWrite(B, 1023 - b.toInt());

  // Send HTTP response
  client.println("HTTP/1.1 200 OK");
  client.println("Content-Type: text/html");
  client.println();
  client.println(beginHTML());
  client.println("<form>");
  client.println(range("r", "R", r.toInt()));
  client.println(range("g", "G", g.toInt()));
  client.println(range("b", "B", b.toInt()));
  client.println(submit("Set LED Color"));
  client.println("</form>");
  client.println(endHTML());

}
```

---

## Exercises &mdash; 4. Use an IoT API to log values from a DHT11 sensor

We'll be using the *ThingSpeak* API to log the values read from the sensor.
ThingSpeak is an API that enables the creation of sensor logging applications
and sharing of data.

<div style="text-align: center;">
  <img src="images/exercise_4.png" width="300" />
</div>

Visit [thingspeak.com](https://thingspeak.com) and create an account if you haven't yet. Create a channel
with two fields and take note of the channel's API **write** key.

---

## Material &mdash; DHT11

<img src="/images/dht11_sensor.jpg" width="300" />

The DHT11 is a basic, ultra low-cost digital temperature and humidity sensor.
It uses a capacitive humidity sensor and a thermistor to measure the surrounding
air, and spits out a digital signal on the data pin (no analog input pins needed).

---

### Basic DHT11 usage: log values to the Serial console

```c
DHT sensor(D1, DHT11); // DHT11 connected to GPIO 5
float hum = 0, temp = 0;

void setup() {
  Serial.begin(115200);
  sensor.begin();
}

void loop() {
  hum = sensor.readHumidity();
  temp = sensor.readTemperature();
  Serial.print("Humidity: ");
  Serial.print(hum);
  Serial.print("%; Temperature: ");
  Serial.print(temp);
  Serial.println("ÂºC");

  delay(20000); // !!!
}
```

---
### Send values to the thingspeak API
```c
#define HTTP_HOST "api.thingspeak.com"
#define HTTP_PORT 80
#define THINGSPEAK_API_KEY "herpderpkey"

void loop() {
  hum = sensor.readHumidity();
  temp = sensor.readTemperature();

  WiFiClient client;
  if (!client.connect(HTTP_HOST, HTTP_PORT)) {
    Serial.println("connection failed");
    return;
  }

  /* post thingspeak update payload to the server */
  String message = String("") + F("field1=") + String(hum) +
    F("&field2=") + String(temp);
  client.print("POST /update HTTP/1.1\r\n");
  client.print("Host: api.thingspeak.com\r\n");
  client.print("Connection: close\r\n");
  client.print("X-ThingSpeakAPIKey: ");
  client.print(THINGSPEAK_API_KEY);
  client.print("\r\n");
  client.print("Content-Type: application/x-www-form-urlencoded\r\n");
  client.print("Content-Length: ");
  client.print(message.length());
  client.print("\r\n\r\n");
  client.print(message);
  client.print("\r\n");

  delay(20000);
}
```
---

class: center, middle

<img src="/images/thingspeak.png" width="600" />

---

## Exercises &mdash; 5. Use physical buttons to increment and decrement a counter

We are going to use two physical buttons to increment a counter and display it
on a web page. In a sense, this is the opposite of exercise 2, where we used a
virtual object to control a physical object.

<div style="text-align: center;">
  <img src="images/exercise_5.png" width="300" />
</div>

In this exercise, we're going to use a physical object to control a virtual
object.

---

---

## Tactile Switches

---

```c
WiFiServer server(80);
byte b1 = LOW;
byte b1_prev = LOW;
byte b2 = LOW;
byte b2_prev = LOW;
int counter = 0;

void setup() {
  // Initialize serial
  Serial.begin(115200);

  // Initialize gpio pins
  pinMode(D1, INPUT);
  pinMode(D2, INPUT);

  // Connect to WiFi network
  connectToWiFi(WLAN_SSID, WLAN_PASS);
  printWifiStatus();

  // Start the TCP server
  server.begin();
  Serial.println("\nServer started");
}

```

---

```c
void loop() {

  // Read value from gpio pins
  b1 = digitalRead(D1);
  b2 = digitalRead(D2);

  // Check which pins go from HIGH to LOW or LOW to HIGH
  if (b1_prev == LOW && b1 == HIGH) {
    counter--;
    Serial.println(counter);
    b1_prev = HIGH;
  } else if (b1_prev == HIGH && b1 == LOW) {
    b1_prev = LOW;
  }

  if (b2_prev == LOW && b2 == HIGH) {
    counter++;
    Serial.println(counter);
    b2_prev = HIGH;
  } else if (b2_prev == HIGH && b2 == LOW) {
    b2_prev = LOW;
  }

  ...
```

---

```c
  ...

  // Check if there are clients connect to our TCP server
  WiFiClient client = server.available();
  if (!client) {
    return;
  }

  // Read (and discard) the client request data
  while (!client.available()) {
    client.read();
  }

  // Send HTTP reply to the client
  client.flush();
  client.println(beginHTML());
  client.print("<h1>");
  client.print(counter);
  client.println("</h1>");
  client.println(endHTML());

}
```
