class: center, middle, inverted, inverted

# Exercises

???

Before the exercises make sure everyone has everything set up.

Have everyone do a basic LED connection to the nodemcu

---

# Setup

Install the Arduino IDE from [https://www.arduino.cc/en/Main/Software](https://www.arduino.cc/en/Main/Software)

Install the CH340G drivers for the development board if needed. Usually, Windows and Linux don't need a manual install.

- Mac OS X Sierra: [https://github.com/adrianmihalko/ch340g-ch34g-ch34x-mac-os-x-driver](https://github.com/adrianmihalko/ch340g-ch34g-ch34x-mac-os-x-driver)

- Linux: [http://www.wch.cn/download/CH341SER_LINUX_ZIP.html](http://www.wch.cn/download/CH341SER_LINUX_ZIP.html)

- Windows: [http://www.wch.cn/download/CH341SER_ZIP.html](http://www.wch.cn/download/CH341SER_ZIP.html)

Now add the ESP8266 capabilities to your Arduino IDE

- Open the Arduino IDE, and under "Preferences" > "Additional Boards Manager URLs" add [http://arduino.esp8266.com/stable/package_esp8266com_index.json](http://arduino.esp8266.com/stable/package_esp8266com_index.json)

- Under Tools &mdash; Boards &mdash; Boards Manager, search for *ESP8266*. Install *esp8266 by ESP8266 community, version 2.3.0*.

???

CH340G is the USB to Serial chip the NodeMCU uses

---

# Connection Parameters

Change the following settings in the Tools menu:

<dl>
  <dt>Board</dt>
  <dd>NodeMCU 1.0 (ESP-12E Module) (only available after last step in setup)</dd>

  <dt>CPU Frequency</dt>
  <dd>80 MHz</dd>

  <dt>Flash Size</dt>
  <dd>4M (3M SPIFFS)</dd>

  <dt>Upload Speed</dt>
  <dd>115200</dd>

  <dt>Port</dt>
  <dd>Depends on your machine. In windows, it's usually a COM port. In Linux, a <pre>/dev/ttyUSBx</pre> device. In OS X, <pre>/dev/wchusbserialsomething</pre>.</dd>
</dl>

---

## Exercise 1 &mdash; Connect to the Internet

Our first exercise won't have a physical circuit. We'll be using `wttr.in`,
an awesome weather forecast service to test our connectivity to the internet.

### NodeMCU Development Board

.center[<img src="/images/nodemcu_pins.png" width="400" />]

???

- Mention the important pins for the workshop: 3.3v src, ground and gpio.

- Mention that the addressing of the GPIO pins is different from the pin
labels on the board

---

## Establishing connectivity to a wireless network

```c
#include <ESP8266WiFi.h>

#define WLAN_SSID "my awesome wlan name"
#define WLAN_PASS "hunter2"

void setup() {
  // Initialize the Serial connection
  Serial.begin(115200);
  Serial.print("Connecting to wireless network ");
  Serial.println(WLAN_SSID);

  // Try connecting to the configured wlan network
  WiFi.begin(WLAN_SSID, WLAN_PASS);
  while(WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  // Print wlan IP address
  IPAddress ip = WiFi.localIP();
  Serial.print("Connected! IP Address: ");
  Serial.println(ip);
}
```

---

### Querying wttr.in

No high-level HTTP clients üòß. We'll do everything through a TCP socket üëå

A simple HTTP request looks like:

```http
GET /Porto?T HTTP/1.1
Host: wttr.in
User-Agent: curl
Connection: close
```
‚ö†Ô∏è Each of the lines above is terminated with CRLF characters! (`\r\n`).
**Don't forget the blank line in the end** that marks the end of the HTTP request.

Try it out! Send these lines to `wttr.in` with `nc` or `telnet`!

```bash
nc wttr.in 80
```

???

The way we'll be using the intenet connectivity isn't the most friendly;
application-layer protocols shouldn't be used like this. Our purpose, though
is to give you a general feel of how everything is at a low level.

---

```c
void loop() {
  // `client` is a TCP client socket which we connect to `wttr.in` in port 80
  WiFiClient client;
  if (!client.connect("wttr.in", 80)) {
    Serial.println("connection failed");
    return;
  }

  // send the HTTP request to the server we just connected to
  client.print("GET /Porto?T HTTP/1.1\r\n");
  client.print("Host: wttr.in\r\n");
  client.print("User-Agent: curl\r\n");
  client.print("Connection: close\r\n");
  client.print("\r\n");

  // wait up to 5 seconds for a response
  unsigned long timeout = millis();
  while (client.available() == 0) {
    if (millis() - timeout > 5000) {
      client.stop();
      return;
    }
  }

  // read all the lines of the reply from server and print them to Serial
  while(client.available()){
    String line = client.readStringUntil('\r');
    Serial.print(line);
  }
  Serial.println();

  // wait before finishing the loop (and starting the next)
  delay(5000);
}
```

---

## Exercise 2 &mdash; Toggle an LED using a web interface

This exercise is a bit different from exercise 1. Instead of using a client socket
to have our microcontroller connect to an internet server, we're using a server
socket so we can have a browser connect to our microcontroller that will act as
an HTTP server.

We'll also set up a small physical circuit using a resistor and an led.

<div class="center">
  <img src="/images/exercise_2.png" width="300" />
</div>

---

## Breadboard

<img src="/images/breadboards.jpg" width="100%" />

---

## LED

<div class="center">
  <img src="/images/led-schematic.png" width="600" />
</div>

???

An LED is a type of diode, and a diode is a device that allows current flow in one
direction only, so polarity is very important. Try connecting the LED directly to
your devkit's power source and gnd.

---

## Resistors

<div class="center">
  <img src="/images/resistors.jpg" width="100%">
</div>

???

100 Ohm and 56 Ohm

---

We'll be using a few functions to reduce the size of the code samples
in this presentation like `connectToWifi` and `printWifiStatus`.

```c
// A TCP server listening on port 80 using the wifi hardware
WiFiServer server(80);

void setup() {
  // Initialize serial I/O
  Serial.begin(115200);

  // Initialize GPIO pins
  pinMode(D6, OUTPUT);

  // Connect to WiFi network
  connectToWiFi(WLAN_SSID, WLAN_PASS);
  printWifiStatus();

  // Start the server
  server.begin();
  Serial.println("\nServer started");
}
```

---

```c
void loop() {
  // Wait until a client connects to our server
  WiFiClient client = server.available();
  if (!client) {
    return;
  }

  // Wait until the connected client sends the request data
  while (!client.available()) {
    delay(1);
  }

  // Get the first line of the HTTP request
  String request = client.readStringUntil('\r');

  // Ignore browser requests for favicon.ico
  if (request.indexOf("favicon.ico") >= 0) {
    return;
  }

  ...
```

---

```c
  ...

  // Parse the query string in the GET line
  // GET /?led=ON

  String led = query(request, "led");
  bool isOn = led.indexOf("ON") > -1;

  // Turn LED on or off
  digitalWrite(LED, isOn ? HIGH : LOW);

  // Send HTTP reply to the client
  client.println("HTTP/1.1 200 OK");
  client.println("Content-Type: text/html");
  client.println();
  client.println("<html>");
  client.println("<body>");
  client.println("<form>");
  client.println(input("hidden", "led", isOn ? "OFF" : "ON"));
  client.println(submit(isOn ? "Turn Off" : "Turn On"));
  client.println("</form>");
  client.println("</body>");
  client.println("</html>");
}

```

---

## Parsing the query string

```c
String query(String request, String variable) {
  int begin = request.indexOf(variable + "=");

  if (begin < 0) return "";

  int end = request.indexOf("&", begin);
  if (end < 0) end = request.indexOf(" ", begin);

  return request.substring(begin + 2, end);
}
```

---

## Exercise 3 &mdash; Control an RGB LED's hue and brightness using a web interface

This exercise is very similar to the previous, with some more added complexity
in the code and in the circuit.

The RGB led we're providing is the "common anode" type, so we wire it like the
following diagram.

<div class="center">
  <img src="/images/exercise_3.png" width="300" />
</div>

---

## RGB LED

<div class="center">
  <img src="/images/common_anode_rgb_led.png">
</div>

A common anode RGB LED has shares a positive terminal with
the red, green and blue cathodes (negative terminals).

<div class="center margin-top">
  <img src="/images/common_cathode_rgb_led.png">
</div>

A common cathode RGB LED has shares a negative terminal with
the red, green and blue anodes (positive terminals).

???

Explain that common cathode leds would have the common cathode connected
to ground and the R, G and B terminals to GPIO pins. In this case a high
state in the GPIO would turn the LED color on.

In common anode type leds the reverse happens: the common anode gets
connected to the voltage source and the R G B terminals to GPIO pins.
A high state in the GPIO would turn the LED color off.

But how are we going to control the brightness of the LED? (next slide)

---

## PWM to the rescue!

<div class="center">
  <img src="/images/rgb-led-pwm.png" width="600" />
</div>

This chart is valid for common anode type RGB LEDS.
Note that for a common cathode type RGB LED, a high state in 10%
of the duty cycle would make the led dimmer and not brighter.

---

This time we'll use a few functions to abstract the printing of HTML
content and the parsing of the query string.


```c
#define R D5
#define G D6
#define B D7

WiFiServer server(80);

void setup() {
  Serial.begin(115200);

  pinMode(R, OUTPUT);
  pinMode(G, OUTPUT);
  pinMode(B, OUTPUT);

  // Connect to WiFi network
  connectToWiFi(WLAN_SSID, WLAN_PASS);
  printWifiStatus();

  // Start the server
  server.begin();
  Serial.println("\nServer started");
}

```

---

```c
void loop() {
  WiFiClient client = server.available();

  if (!client) {
    return;
  }

  // Wait until de client sends some data
  while(!client.available()) {
    delay(1);
  }

  // process request
  String request = client.readStringUntil('\r');

  // ignore browser requests for favicon.ico
  if (request.indexOf("favicon.ico") >= 0) {
    return;
  }

  ...
```

---

```c
  ...

  // Read the R, G and B values from the query string
  String r = query(request, "r");
  String g = query(request, "g");
  String b = query(request, "b");

  Serial.println("(" + r + ", " + g + ", " + b + ")");

  // For commomn anode type RGB LEDS lower values mean a brighter light:
  analogWrite(R, 1023 - r.toInt());
  analogWrite(G, 1023 - g.toInt());
  analogWrite(B, 1023 - b.toInt());

  // Send HTTP response
  client.println("HTTP/1.1 200 OK");
  client.println("Content-Type: text/html");
  client.println();
  client.println(beginHTML());
  client.println("<form>");
  client.println(range("r", "R", r.toInt()));
  client.println(range("g", "G", g.toInt()));
  client.println(range("b", "B", b.toInt()));
  client.println(submit("Set LED Color"));
  client.println("</form>");
  client.println(endHTML());

}
```

---

## Exercise 4 &mdash; Use an IoT API to log values from a DHT11 sensor

We'll be using the *ThingSpeak* API to log the values read from the sensor.
ThingSpeak is an API that enables the creation of sensor logging applications
and sharing of data.

<div class="center">
  <img src="images/exercise_4.png" width="300" />
</div>

???



---

## Material &mdash; DHT11

<div class="center">
  <img src="/images/dht11-2.jpg" width="500" />
</div>

The DHT11 is a basic, ultra low-cost digital temperature and humidity sensor.
It uses a capacitive humidity sensor and a thermistor to measure the surrounding
air, and outputs a digital signal on the data pin.

‚ö†Ô∏è You will need to add an additional library to the Arduino IDE. Go to Sketch &mdash;
Include Library &mdash; Manage Libraries and search for "DHT11". Install "DHT11
sensor library by Adafruit", **version 1.2.3**.

---

# ThingSpeak

Visit [thingspeak.com](https://thingspeak.com) and create an account if you haven't yet. Create a channel
with two fields and take note of the channel's API **write** key.

<div class="center">
  <img src="/images/thingspeak-create-channel.png" width="100%" />
</div>

---

## Basic DHT11 usage: log values to the Serial console

```c
DHT sensor(D1, DHT11); // DHT11 connected to GPIO 5
float hum = 0, temp = 0;

void setup() {
  Serial.begin(115200);
  sensor.begin();
}

void loop() {
  hum = sensor.readHumidity();
  temp = sensor.readTemperature();
  Serial.print("Humidity: ");
  Serial.print(hum);
  Serial.print("%; Temperature: ");
  Serial.print(temp);
  Serial.println("¬∫C");

  delay(20000); // !!!
}
```

---

## Send values to the thingspeak API

```c
#define HTTP_HOST "api.thingspeak.com"
#define HTTP_PORT 80
#define THINGSPEAK_API_KEY "herpderpkey"

void loop() {
  hum = sensor.readHumidity();
  temp = sensor.readTemperature();

  WiFiClient client;
  if (!client.connect(HTTP_HOST, HTTP_PORT)) {
    Serial.println("connection failed");
    return;
  }

  /* post thingspeak update payload to the server */
  String message = String("") + F("field1=") + String(hum) +
    F("&field2=") + String(temp);
  client.print("POST /update HTTP/1.1\r\n");
  client.print("Host: api.thingspeak.com\r\n");
  client.print("Connection: close\r\n");
  client.print("X-ThingSpeakAPIKey: ");
  client.print(THINGSPEAK_API_KEY);
  client.print("\r\n");
  client.print("Content-Type: application/x-www-form-urlencoded\r\n");
  client.print("Content-Length: ");
  client.print(message.length());
  client.print("\r\n\r\n");
  client.print(message);
  client.print("\r\n");

  delay(20000);
}
```
---

class: center, middle

<div class="center">
  <img src="/images/thingspeak.png" width="600" />
</div>

---

## Exercise 5 &mdash; Use physical buttons to increment and decrement a counter

We are going to use two physical buttons to increment a counter and display it
on a web page. In a sense, this is the opposite of exercise 2, where we used a
virtual object to control a physical object.

<div class="center">
  <img src="images/exercise_5.png" width="300" />
</div>

In this exercise, we're going to use a physical object to control a virtual
object.

---

## Tactile Switches

<div class="center">
  <img src="images/pushbuttons.jpg" width="400" />
</div>

<div class="center">
  <img src="images/tactile-switch.jpg" width="400" />
</div>

---

```c
WiFiServer server(80);
byte b1 = LOW;
byte b1_prev = LOW;
byte b2 = LOW;
byte b2_prev = LOW;
int counter = 0;

void setup() {
  // Initialize serial
  Serial.begin(115200);

  // Initialize gpio pins
  pinMode(D1, INPUT);
  pinMode(D2, INPUT);

  // Connect to WiFi network
  connectToWiFi(WLAN_SSID, WLAN_PASS);
  printWifiStatus();

  // Start the TCP server
  server.begin();
  Serial.println("\nServer started");
}

```

---

```c
void loop() {

  // Read value from gpio pins
  b1 = digitalRead(D1);
  b2 = digitalRead(D2);

  // Check which pins go from HIGH to LOW or LOW to HIGH
  if (b1_prev == LOW && b1 == HIGH) {
    counter--;
    Serial.println(counter);
    b1_prev = HIGH;
  } else if (b1_prev == HIGH && b1 == LOW) {
    b1_prev = LOW;
  }

  if (b2_prev == LOW && b2 == HIGH) {
    counter++;
    Serial.println(counter);
    b2_prev = HIGH;
  } else if (b2_prev == HIGH && b2 == LOW) {
    b2_prev = LOW;
  }

  ...
```

---

```c
  ...

  // Check if there are clients connect to our TCP server
  WiFiClient client = server.available();
  if (!client) {
    return;
  }

  // Read (and discard) the client request data
  while (!client.available()) {
    client.read();
  }

  // Send HTTP reply to the client
  client.println("HTTP/1.1 200 OK");
  client.println("Content-Type: text/html");
  client.println();
  client.println(beginHTML());
  client.print("<h1>");
  client.print(counter);
  client.println("</h1>");
  client.println(endHTML());
}
```

---
